load("@io_bazel_rules_go//go:def.bzl", "go_library")

genrule(
    name = "reproxytool_ui_tar_gen",
    srcs = glob(
        include = ["app/**/*"],
        exclude = ["app/dist/**/*"],
    ),
    outs = ["reproxytool_ui.tar"],
    cmd_bash = """
      export PATH=$$PWD/$$(dirname $(NPM_PATH)):$$PWD/$$(dirname $(NODE_PATH)):$$PATH
      export ROOT=$$PWD
      export HOME=/tmp

      echo "Installing angular..."
      cd internal/pkg/logrecordserver/ui/app
      $$ROOT/$(NPM_PATH) ci

      echo "Building app"
      node_modules/.bin/ng build
      cd $$ROOT
      tar cvf $(OUTS) internal/pkg/logrecordserver/ui/app/dist/reproxyui/browser
    """,
    exec_properties = {"dockerNetwork": "standard"},
    target_compatible_with = select({
        "@platforms//os:linux": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
    toolchains = ["@nodejs_toolchains//:resolved_toolchain"],
    tools = [
        "@nodejs//:node",
        "@nodejs//:node_bin",
        "@nodejs//:node_files",
        "@nodejs//:npm",
        "@nodejs//:npm_bin",
        "@nodejs//:npm_files",
        "@nodejs_toolchains//:resolved_toolchain",
    ],
    visibility = ["//visibility:public"],
)

go_library(
    name = "ui",
    srcs = ["ui.go"],
    embedsrcs = [
        "reproxytool_ui.tar",
    ],
    importpath = "github.com/bazelbuild/reclient/internal/pkg/logrecordserver/ui",
    visibility = ["//:__subpackages__"],
)
